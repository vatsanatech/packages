group 'io.flutter.plugins.videoplayer'
version '1.0-SNAPSHOT'
def args = ["-Xlint:deprecation","-Xlint:unchecked","-Werror"]

buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://muxinc.jfrog.io/artifactory/default-maven-release-local"
        }
    }

    dependencies {
        //the gradle version has been upgraded to 7.4.2 because of MUX Data SDK
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        //required for passing customer data in mux analytics
        maven { url 'https://muxinc.jfrog.io/artifactory/default-maven-release-local' }
        //these are extra MUX Data dependencies which may be required later in future
//        maven { url 'https://muxinc.jfrog.io/artifactory/default-maven-virtual' }
//        maven { url 'https://muxinc.jfrog.io/artifactory/default-maven-local' }
    }
}

project.getTasks().withType(JavaCompile){
    options.compilerArgs.addAll(args)
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'io.flutter.plugins.videoplayer'
    }
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        checkAllWarnings true
        warningsAsErrors true
        disable 'AndroidGradlePluginVersion', 'InvalidPackage', 'GradleDependency'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dependencies {
//        def exoplayer_version = "2.18.7"
        // downgraded version of ExoPlayer from 2.18.7 to 2.18.1 because MUX Data has support only till 2.18.1
        def exoplayer_version = "2.18.1"
        implementation "com.google.android.exoplayer:exoplayer-core:${exoplayer_version}"
        implementation "com.google.android.exoplayer:exoplayer-hls:${exoplayer_version}"
        implementation "com.google.android.exoplayer:exoplayer-dash:${exoplayer_version}"
        implementation "com.google.android.exoplayer:exoplayer-smoothstreaming:${exoplayer_version}"
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'androidx.test:core:1.3.0'
        testImplementation 'org.mockito:mockito-inline:5.0.0'
        testImplementation 'org.robolectric:robolectric:4.10.3'
        api 'com.mux.stats.sdk.muxstats:MuxExoPlayer_r2_18_1:3.4.1'
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
